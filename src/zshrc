#!/usr/bin/env zsh

#
# ANTIGEN
#

# export ANTIGEN_DEFAULT_REPO_URL=https://github.com/sharat87/oh-my-zsh.git
source ~/.antigen/antigen.zsh

# Load the oh-my-zsh's library.
antigen-use oh-my-zsh

# Bundles from the default repo declared above.
antigen-bundles <<EOBUNDLES

cake
npm
bower
django
python
git

yonchu/grunt-zsh-completion

# Completions.
zsh-users/zsh-completions src

# Syntax highlighting bundle.
zsh-users/zsh-syntax-highlighting

EOBUNDLES

# This plugin must follow the others to work correctly!
antigen-bundle history-substring-search

# Load the theme.
antigen-theme ~/Dotfiles/src/lettertwo.zsh-theme

# Tell Antigen that we're done.
antigen-apply

#
# ZSH CONFIG
#

# Red dots to be displayed while waiting for completion
COMPLETION_WAITING_DOTS=true

# Don't automatically change the terminal title on chpwd
DISABLE_AUTO_TITLE=true

# Style history substring search matches
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='bg=black,fg=cyan,bold'
# Don't store any commands in history that begin with whitespace
HIST_IGNORE_SPACE=true
# Don't store function definitions in history
HIST_NO_FUNCTIONS=true

#
# HISTIGNORE
#
# A way to do bash-style history ignore for certain commands
HISTIGNORE="&:cd:ls:ll:la:l1:l.:..::pwd:exit:clear:cls"
function zshaddhistory() {
	CMDS=("${(s/:/)HISTIGNORE}")
	for CMD in $CMDS; do
		if [[ $1 == "$CMD"* ]] ; then
			return 1
		fi
	done
}

export EDITOR=/usr/local/bin/subl
export CLICOLOR=1

# directory stack
export DIRSTACKSIZE=9
DIRSTACKFILE=~/.zdirs
if [[ -f $DIRSTACKFILE ]] && [[ $#dirstack -eq 0 ]]; then
  dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
  [[ -d $dirstack[1] ]] && cd $dirstack[1] && cd $OLDPWD
fi
chpwd() {
  print -l $PWD ${(u)dirstack} >$DIRSTACKFILE
}

# path config
export PATH="./node_modules/.bin:~/bin:~/sbin:/usr/local/bin:/usr/local/sbin:$PATH"
cdpath=(~ ~/Projects/ ~/Library/Application\ Support/)

#
# NOCORRECT
#
# theoretically helpful correction is too frequently just annoying :(
# To help reduce annoying false corrections, we keep a .zsh_nocorrect file.
# Any commands listed in that file will not be corrected.
if [ -f ~/.zsh_nocorrect ]; then
	while read -r COMMAND; do
		alias $COMMAND="nocorrect $COMMAND"
	done < ~/.zsh_nocorrect
fi

#
# ADDITIONAL UTILS
#

# node version manager (nvm)
[[ -s ~/.nvm/nvm.sh ]] && source ~/.nvm/nvm.sh


# ruby version manager (rvm)
[[ -s ~/.rvm/scripts/rvm ]] && source ~/.rvm/scripts/rvm

#
# KEYBINDINGS
#

bindkey "^R" history-incremental-pattern-search-backward
bindkey "^S" history-incremental-pattern-search-forward
bindkey "^[[7~" beginning-of-line #Home key
bindkey "^[[8~" end-of-line #End key
bindkey "^[[3~" delete-char #Del key
bindkey "^[[A" history-beginning-search-backward #Up Arrow
bindkey "^[[B" history-beginning-search-forward #Down Arrow
bindkey "^[Oc" forward-word # control + right arrow
bindkey "^[Od" backward-word # control + left arrow
bindkey "^H" backward-kill-word # control + backspace
bindkey "^[[3^" kill-word # control + delete

#
# ALIASES AND FUNCTIONS
#

cwd_prompt_info () {
	echo "${PWD/#$HOME/~}"
}

set_title() {
	if [[ "$TERM" = screen* ]]
	then
		print -Pn "\ek$1q\e\\"
	elif [[ "$TERM" = xterm* ]] || [[ $TERM = rxvt* ]] || [[ "$TERM_PROGRAM" = "iTerm.app" ]]
	then
		print -Pn "\e]0;$1\a"
	fi
}

# Set terminal title to the cwd
case $TERM in
    xterm*)
        precmd () {set_title `cwd_prompt_info`}
        ;;
esac

# zmv '(*).lis' '$1.txt'
# mmv *.c.orig orig/*.c
# mmv **/*2008.mp3 **/*2009.mp3
autoload -U zmv
alias mmv='noglob zmv -W'

# Default top to organize by cpu usage
alias top='top -o cpu'

# Use trash instead of rm
alias rm='trash'

# Pings with 5 packets, not unlimited
alias ping='ping -c 5'
# Disk free, in gigabytes, not bytes
alias df='df -h'
# Calculate total disk usage for a folder
alias du='du -h -c'

# split tab
alias stab='split_tab'

# IP addresses
alias ip="dig +short myip.opendns.com @resolver1.opendns.com"
alias localip="ipconfig getifaddr en1"
alias ips="ifconfig -a | grep -o 'inet6\? \(\([0-9]\+\.[0-9]\+\.[0-9]\+\.[0-9]\+\)\|[a-fA-F0-9:]\+\)' | sed -e 's/inet6* //'"

# Python aliases
alias setup="nocorrect python ./setup.py"
# Show the current python path
alias pypath='python -c "import sys; print sys.path" | tr "," "\n" | grep -v "egg"'

# Django aliases
alias manage="nocorrect python ./manage.py"
alias dumpauth="manage dumpdata --indent=2 auth sessions"
alias syncall="manage syncdb --noinput --all && manage migrate --fake"

PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

# Git aliases
# these are overrides for some of the aliases defined by the omzsh git plugin
alias gs='g st'
alias gl='g l'
alias glr='g lr'
alias gr='g r'
alias gap='g add -p'
alias gdt='g difftool'

alias ts='tig status'

# kennethreitz/autoenv
source /usr/local/opt/autoenv/activate.sh
autoenv_init
