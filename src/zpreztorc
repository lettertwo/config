#!/usr/bin/env zsh

# Make sure custom prompts are available
fpath=(~/.zprompt ~/.zmodules $fpath)

#
# General
#

# Color output (auto set to 'no' on dumb terminals).
zstyle ':prezto:*:*' color 'yes'

# Set the Zsh functions to load (man zshcontrib).
zstyle ':prezto:load' zfunction 'zargs' 'zmv'

# Set the Prezto modules to load (browse modules).
# The order matters.
zstyle ':prezto:load' pmodule \
  'environment' \
  'terminal' \
  'history' \
  'directory' \
  'spectrum' \
  'completion' \
  'simplegit' \
  'node' \
  'ruby' \
  'gulp' \
  'syntax-highlighting' \
  'history-substring-search' \
  'prompt' \
  'autoenv' \

#
# History Substring Search
#
zstyle ':prezto:module:history-substring-search' color 'yes'

# Set the query found color.
zstyle ':prezto:module:history-substring-search:color' found 'bg=black,fg=cyan,bold'

# Set the query not found color.
zstyle ':prezto:module:history-substring-search:color' not-found 'bg=red,fg=black,bold'

#
# Prompt
#

# Set the prompt theme to load.
# Setting it to 'random' loads a random theme.
# Auto set to 'off' on dumb terminals.
zstyle ':prezto:module:prompt' theme 'lettertwo'


#
# Syntax Highlighting
#
# Set syntax highlighters.
# By default, only the main highlighter is enabled.
zstyle ':prezto:module:syntax-highlighting' highlighters \
  'main' \
  'brackets' \
  'pattern' \
  'cursor' \
  'root'
#
# Set syntax highlighting styles.
# zstyle ':prezto:module:syntax-highlighting' styles \
#   'builtin' 'bg=blue' \
#   'command' 'bg=blue' \
#   'function' 'bg=blue'


#
# Terminal
#
# Auto set the tab and window titles.
zstyle ':prezto:module:terminal' auto-title 'yes'

export EDITOR=subl

# ruby version manager (rvm)
[[ -s ~/.rvm/scripts/rvm ]] && source ~/.rvm/scripts/rvm

#
# HISTIGNORE
#
# A way to do bash-style history ignore for certain commands
HISTIGNORE="&:cd:ls:ll:la:l1:l.:..::pwd:exit:clear:cls"
function zshaddhistory() {
    CMDS=("${(s/:/)HISTIGNORE}")
    for CMD in $CMDS; do
        if [[ $1 == "$CMD"* ]] ; then
            return 1
        fi
    done
}

#
# NOCORRECT
#
# theoretically helpful correction is too frequently just annoying :(
# To help reduce annoying false corrections, we keep a .znocorrect file.
# Any commands listed in that file will not be corrected.
if [ -f ~/.znocorrect ]; then
    while read -r COMMAND; do
        alias $COMMAND="nocorrect $COMMAND"
    done < ~/.znocorrect
fi

#
# OPTS
#

unsetopt beep # no bell on error
setopt rm_star_silent # do not ask for confirmation for `rm *' or `rm path/*'
setopt CORRECT # correct commands

#
# KEYBINDINGS
#

# bindkey "^R" history-incremental-pattern-search-backward
# bindkey "^S" history-incremental-pattern-search-forward
bindkey "^[[7~" beginning-of-line #Home key
bindkey "^[[8~" end-of-line #End key
bindkey "^[[3~" delete-char #Del key
bindkey "^[[A" history-beginning-search-backward #Up Arrow
bindkey "^[[B" history-beginning-search-forward #Down Arrow
bindkey "^[Oc" forward-word # control + right arrow
bindkey "^[Od" backward-word # control + left arrow
bindkey "^H" backward-kill-word # control + backspace
bindkey "^[[3^" kill-word # control + delete

#
# ALIASES AND FUNCTIONS
#
if zstyle -t ':prezto:module:utility:ls' color; then
  # Define colors for the completion system.
  export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=36;01:cd=33;01:su=31;40;07:sg=36;40;07:tw=32;40;07:ow=33;40;07:'
  alias ls='ls -G'
else
  alias ls='ls -F'
fi
# Lists in one column, hidden files.
alias l='ls -1A'
# Lists human readable sizes.
alias ll='ls -lh'
# Lists human readable sizes, recursively.
alias lr='ll -R'
# Lists human readable sizes, hidden files.
alias la='ll -A'
# Lists human readable sizes, hidden files through pager.
alias lm='la | "$PAGER"'
# Lists sorted by extension (GNU only).
alias lx='ll -XB'
# Lists sorted by size, largest last.
alias lk='ll -Sr'
# Lists sorted by date, most recent last.
alias lt='ll -tr'
# Lists sorted by date, most recent last, shows change time.
alias lc='lt -c'
# Lists sorted by date, most recent last, shows access time.
alias lu='lt -u'

# Default top to organize by cpu usage
alias top='top -o cpu'
# Pings with 5 packets, not unlimited
alias ping='ping -c 5'
# Disk free, in gigabytes, not bytes
alias df='df -h'
# Calculate total disk usage for a folder
alias du='du -h -c'

# IP addresses
alias ip="dig +short myip.opendns.com @resolver1.opendns.com"
alias localip="ipconfig getifaddr en1"
alias ips="ifconfig -a | grep -o 'inet6\? \(\([0-9]\+\.[0-9]\+\.[0-9]\+\.[0-9]\+\)\|[a-fA-F0-9:]\+\)' | sed -e 's/inet6* //'"

# Python aliases
alias setup="nocorrect python ./setup.py"
# Show the current python path
alias pypath='python -c "import sys; print sys.path" | tr "," "\n" | grep -v "egg"'

# Django aliases
alias manage="nocorrect python ./manage.py"
alias dumpauth="manage dumpdata --indent=2 auth sessions"
alias syncall="manage syncdb --noinput --all && manage migrate --fake"

# Use trash instead of rm
alias rm='trash'

# rage quit!!!
function fuck() {
  if killall -9 "$2"; then
    echo ; echo " (╯°□°）╯︵$(echo "$2"|toilet -f term -F rotate)"; echo
  fi
}
# alias rage quit for current user
alias fu='fuck you'
