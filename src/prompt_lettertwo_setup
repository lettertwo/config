#!/usr/bin/env zsh

#
# Authors:
#   Eric Eldredge <lettertwo@gmail.com>
#
BLACK=59 #4e4e42
RED=204 #fd4485
GREEN=149 #b4e745
YELLOW=186 #e8e38a
BLUE=110 #95b4e2
MAGENTA=204 #f43486
CYAN=117 #85e1f1
WHITE=145 #bfbfbf
LIGHT_WHITE=188 #e6e5e6

# Load dependencies.
pmodload 'helper'

prompt_arrow=" %B%F{white}❯%f%b "
prompt_arrow_alternate=" %B%F{magenta}❯%f%b "
insert_mode="%F{cyan}♥%f${prompt_arrow}"
normal_mode="%F{magenta}♡%f${prompt_arrow_alternate}"

function prompt_lettertwo_pwd {
  _prompt_lettertwo_pwd="${PWD/#$HOME/~}"
}

function prompt_lettertwo_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_lettertwo_pwd

  # Get Git repository information.
  git-info

  # Get virtualenv info.
  virtualenv_prompt_info

  # Update xterm title.
  # \e - escape sequence for escape (ESC)
  # \a - escape sequence for bell (BEL)
  # %n - expands to $USERNAME
  # %m - expands to hostname up to first '.'
  # %~ - expands to directory, replacing $HOME with '~'
  case $TERM in
    xterm*)
      print -Pn "\e]0;%n@%m: %~\a"
      ;;
  esac
}

function virtualenv_prompt_info {
  if [ -z $VIRTUAL_ENV ]; then
    _virtualenv_prompt_info=''
  else
    _virtualenv_prompt_info="${prompt_arrow}%F{yellow}${VIRTUAL_ENV:t}%f"
  fi
}

function prompt_lettertwo_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  export VIRTUAL_ENV_DISABLE_PROMPT=true
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_lettertwo_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format "${insert_mode}"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "${normal_mode}"

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:dirty' format ' %B%F{red}★%f%b'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⇡%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⇣%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '${prompt_arrow}$(coalesce "%b" "%p" "%c")%s%D' \
    'rprompt' '%a%d%r'

  # Define prompts.
  PROMPT='
  %F{black}${_prompt_lettertwo_pwd}%f${_virtualenv_prompt_info}${(e)git_info[prompt]}
  ${editor_info[keymap]}'

  RPROMPT='${git_info[rprompt]} %F{black}%n@%m%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

  # LSCOLORS describes what color to use for which attribute when colors are enabled for ls.
  # This string is a concatenation of pairs of the format fb, where f is the foreground color and b is the background color.
  #
  # The color designators are as follows:
  #
  #    a     black
  #    b     red
  #    c     green
  #    d     brown
  #    e     blue
  #    f     magenta
  #    g     cyan
  #    h     light grey
  #    A     bold black, usually shows up as dark grey
  #    B     bold red
  #    C     bold green
  #    D     bold brown, usually shows up as yellow
  #    E     bold blue
  #    F     bold magenta
  #    G     bold cyan
  #    H     bold light grey; looks like bright white
  #    x     default foreground or background
  #
  # The order of the attributes are as follows:
  #
  #    1.   directory
  #    2.   symbolic link
  #    3.   socket
  #    4.   pipe
  #    5.   executable
  #    6.   block special
  #    7.   character special
  #    8.   executable with setuid bit set
  #    9.   executable with setgid bit set
  #    10.  directory writable to others, with sticky bit
  #    11.  directory writable to others, without sticky bit
  #
  # The default is "exfxcxdxbxegedabagacad", i.e. blue foreground and default background for regular
  # directories, black foreground and red background for setuid executables, etc.
  LSDARK=exfxcxdxbxegedabagacad
  LSBRIGHT=Exdxfxgxcxegedacagabac
  export LSCOLORS=$LSBRIGHT


export FZF_DEFAULT_OPTS="
--color fg:${BLACK},bg:-1,hl:${BLUE},fg+:${LIGHT_WHITE},bg+:-1,hl+:${CYAN}
--color info:${YELLOW},prompt:${GREEN},spinner:${GREEN},pointer:${GREEN},marker:${MAGENTA}
"
}

prompt_lettertwo_setup "$@"
